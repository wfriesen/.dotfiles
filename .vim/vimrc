" Has to be set before colorscheme, so that lightline picks up it's own colorscheme setting
let g:lightline = {'colorscheme': 'solarized'}

runtime bundle/vim-pathogen/autoload/pathogen.vim
execute pathogen#infect()

call glaive#Install()
if has('linux')
  Glaive codefmt google_java_executable="google-java-format --aosp"
else
  Glaive codefmt google_java_executable="java -jar ~/bin/google-java-format.jar --aosp"
endif

augroup autoformat_settings
  autocmd FileType java AutoFormatBuffer google-java-format
augroup END

augroup sql_settings
  au!
  autocmd BufNewFile,BufRead *.vw set filetype=sql

  " Use /*..*/ style comments instead of --
  autocmd FileType sql setlocal commentstring=/\*\ %s\ \*/

  function! g:ExecuteSQLBuffer()
    let pos = getpos(".")
    :1,$DBExecRangeSQL
    call setpos(".", pos)
  endfunction
  autocmd FileType sql nmap <F5> :call g:ExecuteSQLBuffer()<CR>

augroup END

augroup java_settings
  autocmd FileType java setlocal shiftwidth=4 softtabstop=4
  autocmd FileType java nmap <F5> :Make compile<CR>
augroup END

set showtabline=2 " Always show the tabline

set shortmess-=S " Show search count in command line

" Pretty colors
syntax on
filetype plugin indent on
set background=dark
colorscheme solarized

highlight LineNr guifg=#586e75 ctermfg=10
highlight CursorLineNr guifg=#839496 ctermfg=12
highlight Cursor guibg=#dc322f

if has("gui_running")
  if has('win32')
    set guifont=Consolas:h11
  else
    set guifont=Hack\ 16
  endif

  set guioptions-=m "menubar
  set guioptions-=r "scrollbar
  set guioptions-=T "toolbar
  set guioptions-=L "Left-hand scrollbar
  set guioptions+=c "Use console dialogs for simple choices
  set guioptions-=e
  set guicursor+=a:block " Always use block cursor in GUI mode

endif

set lazyredraw " Execute macros faster by not redrawing the screen constantly

set number
set relativenumber
set cursorline

set noequalalways

set backspace=indent,eol,start " Make backspace work like expected

set hidden " Allow hidden buffers with unsaved changes

" Formatting options
set fileformats=unix,dos
set encoding=utf-8
set tabstop=2
set softtabstop=0
set expandtab
set shiftwidth=2
set shiftround " round indents to multiples of shiftwidth
set smarttab
set autoindent
set nojoinspaces

" Ignore case, except if the entered search pattern contains upper case chars
set ignorecase
set smartcase
set infercase

set hlsearch " Highlight search matches
set incsearch " Search as you type. <Esc> still cancels and returns
set gdefault " Always use /g for :s commands
set virtualedit=block " In visual block mode, allow the cursor to move anywhere
set backupcopy=yes " Let's webpack pick up changes
set showmode " Show the mode in command line. statusline shows it anyway, but it'd just be empty otherwise

set laststatus=2 " Always show status line

set wildmenu " Show matches when invoking command line completion
set wildignorecase " Case insensitive filename matching

" Exclude these folders from ctrlp
set wildignore+=*/node_modules/*
set wildignore+=*/$tf/*
set wildignore+=*/target/*

set isfname-=@-@ " Strip out @ from file names. Useful for sqlplus files that call others with @@filename.sql

set sessionoptions-=options " Keeping this on seems to break loading sessions when using plugins

set completeopt=
set completeopt+=menu " Use popup menu, even if there's only one match
set completeopt+=menuone " Show even when there's only one match
set completeopt+=preview " Show some extra info (types, filenames, etc)

set undofile
let &undodir = expand('$HOME/.vim/undo')

let g:netrw_liststyle=1 " Show file size and modification times

let g:slime_target = "tmux"
let g:slime_no_mappings = 1 " Default mappings of <C-c><C-c> slow down using it for cancelling
map <leader>ss :SlimeSend<CR>

command! MvnWriteCompile :w | Mvn compile | redraw!
nnoremap <leader>mc :MvnWriteCompile<CR>

" Use space as leader
nnoremap <Space> <Nop>
let mapleader = "\<Space>"

set cedit=<C-l> " Open the command-line window

" Set to defaults, so that later appends in this file won't constantly
" increase it
set viminfo='100,<50,s10,h,rA:,rB:

" Move cursor in command line mode just like in bash and others. C-e and C-k are
" already mapped by default to move to, and delete until, the end of the line
cnoremap <C-a> <Home>
cnoremap <A-b> <C-Left>
cnoremap <A-f> <C-Right>
cnoremap <C-b> <Left>
cnoremap <C-f> <Right>
cnoremap <C-k> <C-\>estrpart(getcmdline(),0,getcmdpos()-1)<CR>

" Toggle search highlighting
nmap <silent> <leader>nh :set hlsearch!<CR>

" Don't use word boundaries for * and #
nnoremap * g*
nnoremap # g#
" g* and g# for word boundaries
nnoremap g* *
nnoremap g# #

" Easier searching for things like attribute-names-with-hyphens
autocmd FileType xml setlocal iskeyword+=-

" Make * and # in visual mode work like in normal mode
vnoremap <silent> * "zy/<C-R>z<CR>
vnoremap <silent> # "zy?<C-R>z<CR>

" Shift and Control insensitive mapping
nnoremap <C-w>t <C-w>T
nnoremap <C-w><C-t> <C-w>T

" More intuitive split window mappings
nnoremap <C-w><Bar> <C-w>v
nnoremap <C-w>_ <C-w>s

" Use Alt- for scrolling
if has('gui_running')
  let s:alt_key_prefix="<A-"
  let s:alt_key_suffix=">"
else
  let s:alt_key_prefix=""
  let s:alt_key_suffix=""
endif
execute "nnoremap ".s:alt_key_prefix."l".s:alt_key_suffix." zl"
execute "nnoremap ".s:alt_key_prefix."h".s:alt_key_suffix." zh"
execute "nnoremap ".s:alt_key_prefix."L".s:alt_key_suffix." 100zl"
execute "nnoremap ".s:alt_key_prefix."H".s:alt_key_suffix." 100zh"
execute "nnoremap ".s:alt_key_prefix."j".s:alt_key_suffix." <C-e>"
execute "nnoremap ".s:alt_key_prefix."k".s:alt_key_suffix." <C-y>"
execute "nnoremap ".s:alt_key_prefix."J".s:alt_key_suffix." <C-d>"
execute "nnoremap ".s:alt_key_prefix."K".s:alt_key_suffix." <C-u>"

nmap <silent> <leader>gs :Gstatus<CR>
nmap <silent> <leader>ge :Gedit<CR>
nmap <silent> <leader>gd :tabedit % <Bar> :Gdiff<CR>

" execute docmd in all windows, then return cursor to the original window
function! g:WindoReturn(docmd)
  let l:current_winnr = winnr()
  execute ":windo ".a:docmd
  execute l:current_winnr."wincmd w"
endfunction

" Fold operations on all windows in the current tab page
nnoremap <silent> <leader>zR :call g:WindoReturn(":normal zR")<CR>
nnoremap <silent> <leader>zM :call g:WindoReturn(":normal zM")<CR>
nnoremap <silent> <leader>zr :call g:WindoReturn(":normal zr")<CR>
nnoremap <silent> <leader>zm :call g:WindoReturn(":normal zm")<CR>

nnoremap <silent> <leader>wq :windo q<CR>

nmap <silent> <leader>u :UndotreeToggle<CR>
nmap <silent> <leader>t :TagbarToggle<CR>
nmap [c <Plug>GitGutterPrevHunk
nmap ]c <Plug>GitGutterNextHunk

let g:yankring_replace_n_pkey = '<leader>y' " Don't clobber the ctrlp mapping
nnoremap <leader>p :YRShow<CR>

let g:undotree_SetFocusWhenToggle=1 " Focus the undotree window when opening

" Move up/down the same way in some plugin windows
function! g:Undotree_CustomMap()
  nmap <buffer> <C-k> <plug>UndotreeGoNextState
  nmap <buffer> <C-j> <plug>UndotreeGoPreviousState
endfunc
autocmd FileType gitcommit nmap <buffer> <C-k> <C-p>
autocmd FileType gitcommit nmap <buffer> <C-j> <C-n>

" start gvim maximized. Simulates pressing Alt+Space x
if has('win32')
  autocmd GUIEnter * simalt ~x
endif

let g:gitgutter_map_keys = 0
let g:gitgutter_max_signs = 1000
let g:gitgutter_realtime = 0 " For speed, turn off update-as-you-type

let g:prettier#autoformat = 0
autocmd BufWritePre *.jsx,*.js,*.json,*.css,*.scss,*.less,*.graphql Prettier

let g:syntastic_auto_loc_list = 1 " Auto-open/close the error list
let g:syntastic_python_checkers = ['pylint']

let g:startify_enable_special = 0 " Don't show <empty buffer> and <quit>
let g:startify_session_sort = 1 " Sort sessions by modification time
let g:startify_custom_header = [] " Turn off the cowsay fortune header
let g:startify_session_dir = '~/.vim/session'
let &viminfo = &viminfo . ',n' . expand('~/.vim/.viminfo')

" Show sessions at the top. Pressing CR immediately after starting loads the
" latest session
let g:startify_list_order = [
        \ ['   Sessions'],
        \ 'sessions',
        \ ['   MRU Files'],
        \ 'files',
        \ ['   MRU in CWD'],
        \ 'dir',
        \ ]

" Change order to put variables at the top, since that's usually where they are
" defined anyway
let g:tagbar_type_sql = {
    \ 'kinds' : [
        \ 'P:packages:1',
        \ 'v:variables',
        \ 'd:prototypes',
        \ 'c:cursors',
        \ 'f:functions',
        \ 'F:record fields',
        \ 'L:block label',
        \ 'p:procedures',
        \ 's:subtypes',
        \ 't:tables',
        \ 'T:triggers',
        \ 'i:indexes',
        \ 'e:events',
        \ 'U:publications',
        \ 'R:services',
        \ 'D:domains',
        \ 'V:views',
        \ 'n:synonyms',
        \ 'x:MobiLink Table Scripts',
        \ 'y:MobiLink Conn Scripts',
        \ 'z:MobiLink Properties',
    \ ],
\ }
let g:tagbar_compact=1 " Don't show help, just tags
let g:tagbar_autofocus=1 " Focus tagbar when opening
let g:tagbar_sort=0 " Sort by file position, not alphabetically
let g:tagbar_width=30

let g:AutoPairsCenterLine=0 " Don't do zz after CR

" Map q to quit windows
autocmd FileType help,diff,qf nnoremap <buffer> q <C-w>q

nmap <leader>F <Plug>CtrlSFPrompt
let g:ctrlsf_default_view_mode = 'compact'
let g:ctrlsf_auto_focus = {"at" : "start"}
let g:ctrlsf_auto_close = {
  \ "normal" : 0,
  \ "compact": 0
  \ }
let g:ctrlsf_absolute_file_path = 1
let g:ctrlsf_default_root = 'project'
if !exists('g:ctrlsf_ackprg') && filereadable(expand('$HOME/bin/ack'))
  let g:ctrlsf_ackprg = expand('$HOME/bin/ack')
endif

let g:grepper = {}
let g:grepper.dir = 'repo'

let g:ctrlp_working_path_mode='ra' " Start ctrlp from the VCS root (r). Fallback to directory of current file (a)
let g:ctrlp_clear_cache_on_exit=0 " Keep the cache across sessions
let g:ctrlp_open_multiple_files='t' " Open multiple files in tabs, rather than new splits
let g:ctrlp_types = ['fil', 'mru', 'buf'] " Re-order modes. After opening CtrlP, <C-b> now jumps to 'Buffers'

let g:SuperTabCrMapping=1 " Finish completion with CR
let g:SuperTabClosePreviewOnPopupClose=1

" Use omni-completion when possible
let g:SuperTabDefaultCompletionType="context"
autocmd FileType *
  \ if &omnifunc != '' |
  \   call SuperTabChain(&omnifunc, "<c-p>") |
  \ endif
" Cycle the expected way
let g:SuperTabDefaultCompletionType = "<c-n>"

let g:lightline.active = {
      \ 'left': [
      \   [ 'mode', 'paste', ],
      \   [ 'readonly', 'filename', 'modified' ]
      \ ],
      \ 'right': [
      \   [ 'lineinfo' ],
      \   [ 'percent' ],
      \   [ 'fileformat', 'fileencoding', 'filetype' ]
      \ ]
      \ }
let g:lightline.inactive = {
      \ 'left': [ [ 'filename', 'modified' ] ],
      \ 'right': [ [ 'lineinfo' ],
      \            [ 'percent' ] ] }
let g:lightline.component = {'lineinfo' : 'î‚¡ %3l:%-2v'}
let g:lightline.component_function = {
      \ 'readonly': 'LightlineReadonly',
      \ 'fullfilepath': 'LightlineFullfilepath',
      \ 'filename': 'LightlineFilename'
      \ }
let g:lightline.separator = {'left': 'î‚°', 'right': 'î‚²'}
let g:lightline.subseparator = { 'left': 'î‚±', 'right': 'î‚³'}
let g:lightline.subseparator.tab = {'active': [ 'tabnum', 'filename', 'modified' ]}
let g:lightline.tabline = {
      \ 'left': [ [ 'tabs' ] ],
      \ 'right': [ [ 'fullfilepath', 'modified' ] ]
      \ }
function! LightlineReadonly()
  return &readonly ? 'î‚¢' : ''
endfunction
let g:ctrlp_status_func = {
      \ 'main': 'CtrlPStatusFunc_main',
      \ 'prog': 'CtrlPStatusFunc_prog',
      \ }

function! LightlineFilename()
  if expand('%:t') == 'ControlP'
    return g:lightline.ctrlp_prev . ' ' . g:lightline.subseparator.left . ' ' .
          \ g:lightline.ctrlp_item . ' ' . g:lightline.subseparator.left . ' ' .
          \ g:lightline.ctrlp_next
  else
    return '' != expand('%:t') ? expand('%:t') : '[No Name]'
  endif
endfunction

function! CtrlPStatusFunc_main(focus, byfname, regex, prev, item, next, marked)
  let g:lightline.ctrlp_prev = a:prev
  let g:lightline.ctrlp_item = a:item
  let g:lightline.ctrlp_next = a:next
  let g:lightline.ctrlp_marked = a:marked
  return lightline#statusline(0)
endfunction
function! CtrlPStatusFunc_prog(str)
  return lightline#statusline(0)
endfunction
let g:tagbar_status_func = 'TagbarStatusFunc'
function! TagbarStatusFunc(current, sort, fname, ...) abort
  return lightline#statusline(0)
endfunction
function! LightlineFullfilepath()
  return '' != expand('%:.') ? expand('%:.') : '[No Name]'
endfunction

if executable('java') && filereadable(expand('~/bin/eclipse.jdt.ls/plugins/org.eclipse.equinox.launcher_1.5.300.v20190213-1655.jar'))
    au User lsp_setup call lsp#register_server({
        \ 'name': 'eclipse.jdt.ls',
        \ 'cmd': {server_info->[
        \     'java',
        \     '-Declipse.application=org.eclipse.jdt.ls.core.id1',
        \     '-Dosgi.bundles.defaultStartLevel=4',
        \     '-Declipse.product=org.eclipse.jdt.ls.core.product',
        \     '-Dlog.level=ALL',
        \     '-noverify',
        \     '-Dfile.encoding=UTF-8',
        \     '-Xmx1G',
        \     '-jar',
        \     expand('~/bin/eclipse.jdt.ls/plugins/org.eclipse.equinox.launcher_1.5.300.v20190213-1655.jar'),
        \     '-configuration',
        \     expand('~/bin/eclipse.jdt.ls/config_linux/'),
        \     '-data',
        \     getcwd()
        \ ]},
        \ 'whitelist': ['java'],
        \ })
endif

autocmd FileType java nmap gd <plug>(lsp-definition)
autocmd FileType java nmap gr <plug>(lsp-references)
autocmd FileType java setlocal omnifunc=lsp#complete

" https://github.com/dbakker/vim-paragraph-motion/blob/master/plugin/paragraphmotion.vim
" Based on: http://stackoverflow.com/questions/1853025/make-and-ignore-lines-containing-only-whitespace

if exists("g:loaded_paragraphmotion") || &cp
  finish
endif
let g:loaded_paragraphmotion=1

function! ParagraphMove(delta, visual, count)
    normal m'
    normal |
    if a:visual
        normal gv
    endif

    if a:count == 0
        let limit = 1
    else
        let limit = a:count
    endif

    let i = 0
    while i < limit
        if a:delta > 0
            " first whitespace-only line following a non-whitespace character
            let pos1 = search("\\S", "W")
            let pos2 = search("^\\s*$", "W")
            if pos1 == 0 || pos2 == 0
                let pos = search("\\%$", "W")
            endif
        elseif a:delta < 0
            " first whitespace-only line preceding a non-whitespace character
            let pos1 = search("\\S", "bW")
            let pos2 = search("^\\s*$", "bW")
            if pos1 == 0 || pos2 == 0
                let pos = search("\\%^", "bW")
            endif
        endif
        let i += 1
    endwhile
    normal |
endfunction

nnoremap <unique> <silent> } :<C-U>call ParagraphMove( 1, 0, v:count)<CR>
onoremap <unique> <silent> } :<C-U>call ParagraphMove( 1, 0, v:count)<CR>
xnoremap <unique> <silent> } :<C-U>call ParagraphMove( 1, 1, v:count)<CR>
nnoremap <unique> <silent> { :<C-U>call ParagraphMove(-1, 0, v:count)<CR>
onoremap <unique> <silent> { :<C-U>call ParagraphMove(-1, 0, v:count)<CR>
xnoremap <unique> <silent> { :<C-U>call ParagraphMove(-1, 1, v:count)<CR>

" Macro to generate java import statement.
" Yank the word under cursor, go to top of file, move one paragraph
" down, insert 'import ', paste the word, move to the beginning of
" the word, go to it's definition, go to top of file and search
" 'package'. Go to end of line, then beginning of WORD, yank until
" a ; character, close the window, Paste, append a dot (.), then jump
" back to where we were.
"
" yiwgg}oimport ";B]gg/package$Byt;qPa.

" Detect the sqlplus Windows binary from within Git Bash
let g:dbext_default_use_win32_filenames = 1
